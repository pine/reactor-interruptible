// ----- Build script -------------------------------------

buildscript {
    ext {
        // Project details
        version = '0.1.1'
        description = 'Reactor InterruptedException utilities.'
        group = 'moe.pine'
        websiteUrl = 'https://github.com/pine/reactor-interruptible'
        gitUrl = 'https://github.com/pine/reactor-interruptible.git'

        // Dependencies
        reactorVersion = '3.4.7'
        junitVersion = '5.7.2'
        mockitoVersion = '3.10.0'
    }
    repositories {
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'gradle.plugin.com.github.spotbugs.snom:spotbugs-gradle-plugin:4.7.1'
        classpath 'net.rdrei.android.buildtimetracker:gradle-plugin:0.11.0'
    }
}


// ----- Project ------------------------------------------

apply plugin: 'java-library'
apply plugin: 'eclipse'
apply plugin: 'idea'

group 'moe.pine'
version rootProject.ext.version
description rootProject.ext.description

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-Xlint:unchecked'
    options.compilerArgs << '-Xlint:deprecation'
}

test {
    useJUnitPlatform()
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "io.projectreactor:reactor-core:$reactorVersion"

    // FindBugs
    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
    testCompileOnly 'com.google.code.findbugs:jsr305:3.0.2'

    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
    testImplementation 'org.assertj:assertj-core:3.20.2'
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
}


// ----- Build Time Tracker -------------------------------

apply plugin: 'build-time-tracker'

buildtimetracker {
    reporters {
        summary {
            ordered true
            barstyle 'ascii'
        }
    }
}


// ----- JaCoCo ---------------------------------------

apply plugin: 'jacoco'

jacoco {
    toolVersion = '0.8.7'
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
    }
}


// ----- SpotBugs -----------------------------------------

apply plugin: 'com.github.spotbugs'

spotbugs {
    toolVersion = '4.2.3'
    excludeFilter = file("${project.rootDir}/findbugs-exclude.xml")
}
spotbugsMain {
    reports {
        xml.required = false
        html.required = true
    }
}
spotbugsTest {
    enabled = false
}


// ----- PMD ----------------------------------------------

apply plugin: 'pmd'

pmd {
    toolVersion = '6.35.0'
}
pmdTest {
    enabled = false
}


// ----- Publish --------------------------------------

apply from: "${rootProject.projectDir}/gradle/sonatype.gradle"
